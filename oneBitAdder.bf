[ 
  1 bit adder by tihbe 
  http://github.com/tihbe
]

+++++
[
  >++++
  [
    >++
    >+++
    >++++
    >+++++
    >++++++
    <<<<<-
  ]
  <-
]

Cells:  0*  0  40  60  80  100  120

E 	>>>>-----------.
n 	>++++++++++.
t 	>----.
e 	<---------.
r 	>--.
_SPACE_ <<<<--------.
2 	>----------.
_SPACE_ <.
c 	>>>--.
o 	>---.
n 	-.
s 	+++++.
e 	<++.
c 	--.
u 	>++.
t 	-.
i 	<++++++.
v 	>++.
e 	<----.
_SPACE_ <<<.
b 	>>>---.
i 	+++++++.
t 	>--.
s 	-.
: 	<<<++++++++.

End of display

>>>>		
,>,

Result: 
Cells: 0  0  32  58  69  105  115  B0  B1*

>++++++
[
  <-------		B1 = B1 - 48
  <-------		B0 = B0 - 48
  >>-
]
<

Doing the maths:
[
  >>+<<-<
  [
    >>+>-<<<-
  ]
  >
]

<
[
  >>>+<<<-
]

Showing the solution: 
>++++++
[
  >++++++++
  >++++++++
  <<-
]

>.>.
